set(cr_package_version "@CREATIVITY_VERSION@")
if(NOT "@CMAKE_BUILD_TYPE@" STREQUAL "Release")
    execute_process(COMMAND git rev-parse --short=10 "@"
        RESULT_VARIABLE cr_git_rev_result
        OUTPUT_VARIABLE cr_git_rev
        OUTPUT_STRIP_TRAILING_WHITESPACE
        )
    if(NOT cr_git_rev_result EQUAL 0)
        message(FATAL_ERROR "Unable to determine git revision tag for non-Release build")
    endif()
    execute_process(COMMAND git show -s "--format=%ci" "@"
        RESULT_VARIABLE cr_build_date_result
        OUTPUT_VARIABLE cr_build_date
        OUTPUT_STRIP_TRAILING_WHITESPACE
        )
    if(NOT cr_build_date_result EQUAL 0)
        message(FATAL_ERROR "Unable to determine commit date for non-Release build")
    endif()
    string(REGEX REPLACE "^([0-9][0-9][0-9][0-9])-([0-9][0-9])-([0-9][0-9]).*" "\\1\\2\\3" cr_build_datestr "${cr_build_date}")
    set(cr_package_version "${cr_package_version}~git${cr_build_datestr}~${cr_git_rev}")
endif()
set(arch ${CMAKE_HOST_SYSTEM_PROCESSOR})
if (arch STREQUAL x86_64)
    set(arch amd64)
endif()
set(GUI @GUI@)
if (GUI)
    set(CPACK_PACKAGE_NAME "creativity-gui")
    set(CPACK_DEBIAN_PACKAGE_CONFLICTS "creativity")
else()
    set(CPACK_PACKAGE_NAME "creativity")
    set(CPACK_DEBIAN_PACKAGE_CONFLICTS "creativity-gui")
endif()
set(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}_${cr_package_version}_${arch}")
set(CPACK_DEBIAN_PACKAGE_VERSION "${cr_package_version}")

